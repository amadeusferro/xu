<program> ::= <declaration>*

<declaration> ::= <variable_declaration>
                | <statement>

<variable_declaration> ::= "const"? <type> <identifier> ("=" <expression>)?
                         | "auto" <identifier> "=" <expression>

<type> ::= <identifier> | <array_type>

<array_type> ::= <type> "[" <int_literal> "]"

<statement> ::= <if_statement>
              | <while_statement>
              | <block_statement>
              | <expression_statement>

<if_statement> ::= "if" "(" <expression> ")" <statement> ("else" <statement>)?

<while_statement> ::= "while" "(" <expression> ")" <statement>

<block_statement> ::= "{" <declaration>* "}"

<expression_statement> ::= <expression>

<expression> ::= <assignment>

<assignment> ::= <ternary> ("=" <assignment>)?

<ternary> ::= "if" "(" <expression> ")" <expression> "else" <expression>
            | <logical_or>

<logical_or> ::= <logical_and> ("||" <logical_and>)*

<logical_and> ::= <equality> ("&&" <equality>)*

<equality> ::= <comparison> (("==" | "!=") <comparison>)*

<comparison> ::= <term> ((">" | ">=" | "<" | "<=") <term>)*

<term> ::= <factor> (("+" | "-") <factor>)*

<factor> ::= <unary> (("*" | "/") <unary>)*

<unary> ::= ("-" | "!") <primary>
          | <primary>

<primary> ::= <literal>
            | <identifier>
            | "(" <expression> ")"

<literal> ::= <int_literal>
            | <float_literal>
            | <string_literal>
            | "true"
            | "false"

<identifier> ::= <alphanumeric_string>

<int_literal> ::= <digits>

<float_literal> ::= <digits> "." <digits>

<string_literal> ::= "\"" <string_char>* "\""

<string_char> ::= any character other than double quotes

<digits> ::= <digit>+

<digit> ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"

<alphanumeric_string> ::= <letter> (<letter> | <digit>)*

<letter> ::= "a" | ... | "z" | "A" | ... | "Z"
